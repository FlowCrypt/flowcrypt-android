/*
 * Â© 2016-2019 FlowCrypt Limited. Limitations apply. Contact human@flowcrypt.com
 * Contributors: DenBond7
 */

import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

def keystoreProperties = new Properties()
File propertiesFile = project.file("keystore.properties")
if (propertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(propertiesFile))
}

android {
    if (projects.hasProperty('devBuild')) {
        splits.abi.enable = false
        splits.density.enable = false
        aaptOptions.cruncherEnable = false
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.flowcrypt.email"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.appVersionCode
        versionName rootProject.ext.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_STL=c++_shared"
            }
        }
        ndk {
            abiFilters "armeabi-v7a", "x86", "x86_64"
        }
    }

    signingConfigs {
        release {
            def keyStoreFile = keystoreProperties['storeFile']
            def keyStorePass = keystoreProperties['storePassword']
            def keySignAlias = keystoreProperties['keyAlias']
            def keyPass = keystoreProperties['keyPassword']

            if (project.hasProperty('runtimeSign')) {
                if (project.hasProperty('storeFile')) {
                    keyStoreFile = project.property("storeFile")
                }

                if (project.hasProperty('storePassword')) {
                    keyStorePass = project.property("storePassword")
                }

                if (project.hasProperty('keyAlias')) {
                    keySignAlias = project.property("keyAlias")
                }

                if (project.hasProperty('keyPassword')) {
                    keyPass = project.property("keyPassword")
                }
            }

            storeFile keyStoreFile != null ? file(keyStoreFile) : file("fix me...")
            storePassword keyStorePass
            keyAlias keySignAlias
            keyPassword keyPass
        }

        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "flowcryptdebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_ACRA_ENABLED', 'true'
            buildConfigField 'boolean', 'IS_MAIL_DEBUG_ENABLED', 'false'
            resValue 'string', 'application_id', android.defaultConfig.applicationId
            resValue 'string', 'gradle_is_acra_enabled', 'true'
            resValue 'string', 'gradle_is_mail_debug_enabled', 'false'
        }

        debug {
            versionNameSuffix '_' + defaultConfig.versionCode + '__' + new SimpleDateFormat('yyyy_MM_dd').format(new Date())
            applicationIdSuffix '.debug'
            resValue 'string', 'application_id', android.defaultConfig.applicationId + applicationIdSuffix
            signingConfig signingConfigs.debug
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    // If you want Gradle to package prebuilt native libraries
    // with your APK, modify the default source set configuration
    // to include the directory of your prebuilt .so files as follows.
    sourceSets {
        main {
            jniLibs.srcDirs 'libs/libnode/bin/'
        }
    }

    flavorDimensions "standard"

    productFlavors {
        //This flavor must be used only for a development.
        // It has settings for a fast building (some features are disabled or not included).
        dev {
            signingConfig signingConfigs.debug
            buildConfigField 'boolean', 'IS_ACRA_ENABLED', 'false'
            buildConfigField 'boolean', 'IS_MAIL_DEBUG_ENABLED', 'true'
            resValue 'string', 'gradle_is_acra_enabled', 'false'
            resValue 'string', 'gradle_is_mail_debug_enabled', 'true'
            versionNameSuffix "_dev"
            dimension "standard"
            minSdkVersion rootProject.ext.minSdkVersion
            resConfigs "en", "xxhdpi"
        }

        //This is a production flavor
        prod {
            signingConfig signingConfigs.release
            buildConfigField 'boolean', 'IS_ACRA_ENABLED', 'true'
            buildConfigField 'boolean', 'IS_MAIL_DEBUG_ENABLED', 'false'
            resValue 'string', 'gradle_is_acra_enabled', 'true'
            resValue 'string', 'gradle_is_mail_debug_enabled', 'false'
            dimension "standard"
        }

        //This flavor must be used only for a testing
        devTest {
            signingConfig signingConfigs.debug
            buildConfigField 'boolean', 'IS_ACRA_ENABLED', 'false'
            buildConfigField 'boolean', 'IS_MAIL_DEBUG_ENABLED', 'false'
            resValue 'string', 'gradle_is_acra_enabled', 'false'
            resValue 'string', 'gradle_is_mail_debug_enabled', 'false'
            versionNameSuffix "_devTest"
            dimension "standard"
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibility
        targetCompatibility rootProject.ext.javaSourceCompatibility
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    tasks.register('renameReleaseBuild') {
        doLast {
            applicationVariants.all { variant ->
                if (variant.buildType.buildType == buildTypes.release) {
                    variant.outputs.each { output ->
                        def file = output.outputFile
                        def newName = file.name.replace(".apk", "_" + defaultConfig.versionCode +
                                "_" + defaultConfig.versionName + "_"
                                + new SimpleDateFormat('yyyy_MM_dd_HH_mm').format(new Date()) + ".apk")
                        output.outputFile.renameTo(new File(file.parent, newName))
                    }
                }
            }
        }
    }

    afterEvaluate {
        Task task = tasks.findByName('packageProdRelease')
        if (task != null) {
            task.finalizedBy renameReleaseBuild
        }
    }
}

configurations {
    prodDebugImplementation {}
    prodReleaseImplementation {}
}

dependencies {
    prodDebugImplementation "com.squareup.leakcanary:leakcanary-android:${rootProject.ext.leakcanaryVersion}"
    prodReleaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${rootProject.ext.leakcanaryVersion}"

    androidTestImplementation "androidx.test.espresso:espresso-core:${rootProject.ext.espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${rootProject.ext.espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${rootProject.ext.espressoVersion}"
    androidTestImplementation "androidx.test.ext:junit:1.1.0"

    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${rootProject.ext.leakcanaryVersion}"
    testImplementation "junit:junit:${rootProject.ext.junitVersion}"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.legacy:legacy-support-v4:${rootProject.ext.androidxBaseVersion}"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation "androidx.legacy:legacy-preference-v14:${rootProject.ext.androidxBaseVersion}"
    implementation "androidx.cardview:cardview:${rootProject.ext.androidxBaseVersion}"
    implementation "androidx.browser:browser:${rootProject.ext.androidxBaseVersion}"
    implementation "androidx.recyclerview:recyclerview:${rootProject.ext.androidxBaseVersion}"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.1.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    implementation 'com.google.android.gms:play-services-base:16.1.0'
    implementation "com.google.android.gms:play-services-auth:${rootProject.ext.playServicesVersion}"
    implementation "com.google.android.material:material:${rootProject.ext.androidxBaseVersion}"

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.api-client:google-api-client-android:1.26.0'
    implementation 'com.google.apis:google-api-services-gmail:v1-rev96-1.25.0'

    implementation "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${rootProject.ext.okhttpVersion}"

    implementation "com.sun.mail:android-mail:${rootProject.ext.javaMailVersion}"
    implementation("com.sun.mail:gimap:${rootProject.ext.javaMailVersion}") {
        //exclude group: 'com.sun.mail' because it exists in 'com.sun.mail:android-mail'
        exclude group: 'com.sun.mail'
    }

    implementation "com.github.bumptech.glide:glide:${rootProject.ext.glideVersion}"
    annotationProcessor "com.github.bumptech.glide:compiler:${rootProject.ext.glideVersion}"
    annotationProcessor "androidx.annotation:annotation:${rootProject.ext.androidxBaseVersion}"

    implementation 'com.eclipsesource.j2v8:j2v8:4.8.2@aar'
    implementation 'commons-io:commons-io:2.6'
    prodImplementation 'ch.acra:acra:4.11'
    implementation 'com.hootsuite.android:nachos:1.1.1'
    implementation 'com.nulab-inc:zxcvbn:1.2.5'
    implementation 'com.madgag.spongycastle:pkix:1.54.0.0'
}
